plugins {
	id 'java'
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '6.1.0'
	id 'com.github.hierynomus.license' version '0.16.1'
	id 'com.palantir.git-version' version '0.12.3'
	id 'com.diffplug.spotless' version '5.14.2'
}

allprojects {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			url 'https://jitpack.io'
		}
		maven {
			name 'sponge'
			url 'https://repo.spongepowered.org/repository/maven-public/'
		}
		maven {
			name 'CodeMC'
			url 'https://repo.codemc.org/repository/maven-public/'
		}
		maven {
			url "https://libraries.minecraft.net"
		}
		maven {
			name 'Fabric'
			url 'https://maven.fabricmc.net/'
		}
	}

	compileJava.options.compilerArgs.add '-parameters'
	compileTestJava.options.compilerArgs.add '-parameters'

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	apply plugin: 'java'
	apply plugin: 'java-library'

	group = 'de.bluecolored.bluemap'
	version = coreVersion

	apply plugin: 'license'
	license {
		header rootProject.file('HEADER')
		excludes([
				'**/*.conf',
				'**/*.yml',
				'**/*.zip',
				'**/*.json'
		])
	}
}

subprojects {
	apply plugin: 'com.github.johnrengelman.shadow'
	apply plugin: 'java'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}


spotless {
	java {
		indentWithSpaces()
		trimTrailingWhitespace()

		target fileTree(project.rootDir) {
			include '**/*.java'
			exclude '**/build/**'
			exclude 'BlueMapAPI/**'
		}
	}
}
